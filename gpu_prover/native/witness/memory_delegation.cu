#include "layout.cuh"
#include "memory.cuh"
#include "trace_delegation.cuh"

using namespace ::airbender::witness::layout;
using namespace ::airbender::witness::memory;
using namespace ::airbender::witness::trace::delegation;

namespace airbender::witness::memory::delegation {

#define MAX_REGISTER_AND_INDIRECT_ACCESSES_COUNT 4

struct RegisterAndIndirectAccessDescriptions {
  const u32 count;
  const RegisterAndIndirectAccessDescription descriptions[MAX_REGISTER_AND_INDIRECT_ACCESSES_COUNT];
};

struct DelegationMemorySubtree {
  const DelegationProcessingLayout delegation_processor_layout;
  const RegisterAndIndirectAccessDescriptions register_and_indirect_access_descriptions;
};

template <typename DESCRIPTION>
DEVICE_FORCEINLINE void process_delegation_requests_execution(const DelegationProcessingLayout &delegation_processor_layout,
                                                              const DelegationTrace<DESCRIPTION> &oracle, const matrix_setter<bf, st_modifier::cg> memory,
                                                              const unsigned index) {
  const auto [multiplicity, abi_mem_offset_high_column, write_timestamp_columns] = delegation_processor_layout;
  const bool execute_delegation_value = oracle.get_witness_from_placeholder_bool({ExecuteDelegation}, index);
  write_bool_value(multiplicity, execute_delegation_value, memory);
  PRINT_U16(M, multiplicity, execute_delegation_value);
  const u16 abi_mem_offset_high_value = oracle.get_witness_from_placeholder_u16({DelegationABIOffset}, index);
  write_u16_value(abi_mem_offset_high_column, abi_mem_offset_high_value, memory);
  PRINT_U16(M, abi_mem_offset_high_column, abi_mem_offset_high_value);
  const TimestampData delegation_write_timestamp_value = oracle.get_witness_from_placeholder_ts({DelegationWriteTimestamp}, index);
  write_timestamp_value(write_timestamp_columns, delegation_write_timestamp_value, memory);
  PRINT_TS(M, write_timestamp_columns, delegation_write_timestamp_value);
}

template <bool COMPUTE_WITNESS, typename DESCRIPTION>
DEVICE_FORCEINLINE void process_indirect_memory_accesses(const RegisterAndIndirectAccessDescriptions &register_and_indirect_access_descriptions,
                                                         const RegisterAndIndirectAccessTimestampComparisonAuxVars &aux_vars,
                                                         const DelegationTrace<DESCRIPTION> &oracle, const matrix_setter<bf, st_modifier::cg> memory,
                                                         const matrix_setter<bf, st_modifier::cg> witness, const unsigned index) {
  const TimestampData write_timestamp = COMPUTE_WITNESS ? oracle.get_witness_from_placeholder_ts({DelegationWriteTimestamp}, index) : TimestampData{};
#pragma unroll
  for (u32 i = 0; i < MAX_REGISTER_AND_INDIRECT_ACCESSES_COUNT; ++i) {
    if (i == register_and_indirect_access_descriptions.count)
      break;
    const auto register_and_indirect_access = &register_and_indirect_access_descriptions.descriptions[i];
    const auto [register_tag, register_payload] = register_and_indirect_access->register_access;
    u32 register_index = 0;
    ColumnSet<NUM_TIMESTAMP_COLUMNS_FOR_RAM> register_read_timestamp_columns = {};
    ColumnSet<REGISTER_SIZE> register_read_value_columns = {};
    switch (register_tag) {
    case RegisterReadAccess: {
      const auto [index, read_timestamp, read_value] = register_payload.register_access_columns_read_access;
      register_index = index;
      register_read_timestamp_columns = read_timestamp;
      register_read_value_columns = read_value;
      break;
    }
    case RegisterWriteAccess: {
      const auto access = register_payload.register_access_columns_write_access;
      register_index = access.register_index;
      register_read_timestamp_columns = access.read_timestamp;
      register_read_value_columns = access.read_value;
      break;
    }
    }
    const TimestampData register_read_timestamp_value = oracle.get_witness_from_placeholder_ts({DelegationRegisterReadTimestamp, register_index}, index);
    write_timestamp_value(register_read_timestamp_columns, register_read_timestamp_value, memory);
    PRINT_TS(M, register_read_timestamp_columns, register_read_timestamp_value);
    const u32 register_read_value = oracle.get_witness_from_placeholder_u32({DelegationRegisterReadValue, register_index}, index);
    write_u32_value(register_read_value_columns, register_read_value, memory);
    PRINT_U32(M, register_read_value_columns, register_read_value);
    if (register_tag == RegisterWriteAccess) {
      const auto register_write_access_columns = register_payload.register_access_columns_write_access.write_value;
      const u32 register_write_value = oracle.get_witness_from_placeholder_u32({DelegationRegisterWriteValue, register_index}, index);
      write_u32_value(register_write_access_columns, register_write_value, memory);
      PRINT_U32(M, register_write_access_columns, register_write_value);
    }
    const auto borrow_set = &aux_vars.aux_borrow_sets[i];
    const auto indirects = borrow_set->indirects;
    if (COMPUTE_WITNESS) {
      const auto borrow_address = borrow_set->borrow;
      const u32 read_timestamp_low = register_read_timestamp_value.get_low();
      const u32 write_timestamp_low = write_timestamp.get_low();
      const bool intermediate_borrow = TimestampData::sub_borrow(read_timestamp_low, write_timestamp_low).y;
      write_bool_value(borrow_address, intermediate_borrow, witness);
      PRINT_U16(W, borrow_address, intermediate_borrow);
    }
    const u32 base_address = register_read_value;
    const auto indirect_accesses_count = register_and_indirect_access->indirect_accesses_count;
    const auto indirect_accesses = register_and_indirect_access->indirect_accesses;
#pragma unroll
    for (u32 access_index = 0; access_index < MAX_INDIRECT_ACCESS_DESCRIPTION_INDIRECT_ACCESSES_COUNT; ++access_index) {
      if (access_index == indirect_accesses_count)
        break;
      const auto [tag, payload] = indirect_accesses[access_index];
      ColumnSet<NUM_TIMESTAMP_COLUMNS_FOR_RAM> read_timestamp_columns = {};
      ColumnSet<REGISTER_SIZE> read_value_columns = {};
      ColumnSet<1> address_derivation_carry_bit_column = {};
      OptionU32::Option<IndirectAccessVariableDependency> variable_dependent = {};
      switch (tag) {
      case IndirectReadAccess: {
        const auto access = payload.indirect_access_columns_read_access;
        read_timestamp_columns = access.read_timestamp;
        read_value_columns = access.read_value;
        address_derivation_carry_bit_column = access.address_derivation_carry_bit;
        variable_dependent = access.variable_dependent;
        break;
      }
      case IndirectWriteAccess: {
        const auto access = payload.indirect_access_columns_write_access;
        read_timestamp_columns = access.read_timestamp;
        read_value_columns = access.read_value;
        address_derivation_carry_bit_column = access.address_derivation_carry_bit;
        variable_dependent = access.variable_dependent;
        break;
      }
      }
      const TimestampData read_timestamp_value =
          oracle.get_witness_from_placeholder_ts({DelegationIndirectReadTimestamp, {register_index, access_index}}, index);
      write_timestamp_value(read_timestamp_columns, read_timestamp_value, memory);
      PRINT_TS(M, read_timestamp_columns, read_timestamp_value);
      const u32 read_value_value = oracle.get_witness_from_placeholder_u32({DelegationIndirectReadValue, {register_index, access_index}}, index);
      write_u32_value(read_value_columns, read_value_value, memory);
      PRINT_U32(M, read_value_columns, read_value_value);
      if (tag == IndirectWriteAccess) {
        const u32 write_value_value = oracle.get_witness_from_placeholder_u32({DelegationIndirectWriteValue, {register_index, access_index}}, index);
        const auto write_value_columns = payload.indirect_access_columns_write_access.write_value;
        write_u32_value(write_value_columns, write_value_value, memory);
        PRINT_U32(M, write_value_columns, write_value_value);
      }
      if (address_derivation_carry_bit_column.num_elements != 0) {
        const u32 derived_address = base_address + access_index * sizeof(u32);
        const bool carry_bit = derived_address >> 16 != base_address >> 16;
        write_u16_value(address_derivation_carry_bit_column, carry_bit, memory);
        PRINT_U16(M, address_derivation_carry_bit_column, carry_bit);
      }
      if (variable_dependent.tag == OptionU32::Some) {
        const auto dependency = variable_dependent.value;
        const u16 offset = oracle.get_witness_from_placeholder_u16({DelegationIndirectAccessVariableOffset, dependency.index}, index);
        write_u16_value(dependency.variable, offset, memory);
        PRINT_U16(M, dependency.variable, offset);
      }
      if (!COMPUTE_WITNESS)
        continue;
      const auto borrow_address = indirects[access_index];
      const u32 read_timestamp_low = read_timestamp_value.get_low();
      const u32 write_timestamp_low = write_timestamp.get_low();
      const bool intermediate_borrow = TimestampData::sub_borrow(read_timestamp_low, write_timestamp_low).y;
      write_bool_value(borrow_address, intermediate_borrow, witness);
      PRINT_U16(W, borrow_address, intermediate_borrow);
    }
  }
}

template <bool COMPUTE_WITNESS, typename DESCRIPTION>
DEVICE_FORCEINLINE void generate(const DelegationMemorySubtree &subtree, const RegisterAndIndirectAccessTimestampComparisonAuxVars &aux_vars,
                                 const DelegationTrace<DESCRIPTION> &oracle, matrix_setter<bf, st_modifier::cg> memory,
                                 matrix_setter<bf, st_modifier::cg> witness, const unsigned count) {
  const unsigned gid = blockIdx.x * blockDim.x + threadIdx.x;
  if (gid >= count)
    return;
  memory.add_row(gid);
  witness.add_row(gid);
  process_delegation_requests_execution(subtree.delegation_processor_layout, oracle, memory, gid);
  process_indirect_memory_accesses<COMPUTE_WITNESS>(subtree.register_and_indirect_access_descriptions, aux_vars, oracle, memory, witness, gid);
}

EXTERN __global__ void ab_generate_memory_values_bigint_with_control_kernel(const __grid_constant__ DelegationMemorySubtree subtree,
                                                                            const __grid_constant__ BigintWithControlOracle oracle,
                                                                            const matrix_setter<bf, st_modifier::cg> memory, const unsigned count) {
  generate<false>(subtree, {}, oracle, memory, memory, count);
}

EXTERN __global__ void ab_generate_memory_and_witness_values_bigint_with_control_kernel(
    const __grid_constant__ DelegationMemorySubtree subtree, const __grid_constant__ RegisterAndIndirectAccessTimestampComparisonAuxVars aux_vars,
    const __grid_constant__ BigintWithControlOracle oracle, const matrix_setter<bf, st_modifier::cg> memory, const matrix_setter<bf, st_modifier::cg> witness,
    const unsigned count) {
  generate<true>(subtree, aux_vars, oracle, memory, witness, count);
}

EXTERN __global__ void ab_generate_memory_values_blake2_with_compression_kernel(const __grid_constant__ DelegationMemorySubtree subtree,
                                                                                const __grid_constant__ Blake2WithCompressionOracle oracle,
                                                                                const matrix_setter<bf, st_modifier::cg> memory, const unsigned count) {
  generate<false>(subtree, {}, oracle, memory, memory, count);
}

EXTERN __global__ void ab_generate_memory_and_witness_values_blake2_with_compression_kernel(
    const __grid_constant__ DelegationMemorySubtree subtree, const __grid_constant__ RegisterAndIndirectAccessTimestampComparisonAuxVars aux_vars,
    const __grid_constant__ Blake2WithCompressionOracle oracle, const matrix_setter<bf, st_modifier::cg> memory,
    const matrix_setter<bf, st_modifier::cg> witness, const unsigned count) {
  generate<true>(subtree, aux_vars, oracle, memory, witness, count);
}

EXTERN __global__ void ab_generate_memory_values_keccak_special5_kernel(const __grid_constant__ DelegationMemorySubtree subtree,
                                                                        const __grid_constant__ KeccakSpecial5Oracle oracle,
                                                                        const matrix_setter<bf, st_modifier::cg> memory, const unsigned count) {
  generate<false>(subtree, {}, oracle, memory, memory, count);
}

EXTERN __global__ void ab_generate_memory_and_witness_values_keccak_special5_kernel(
    const __grid_constant__ DelegationMemorySubtree subtree, const __grid_constant__ RegisterAndIndirectAccessTimestampComparisonAuxVars aux_vars,
    const __grid_constant__ KeccakSpecial5Oracle oracle, const matrix_setter<bf, st_modifier::cg> memory, const matrix_setter<bf, st_modifier::cg> witness,
    const unsigned count) {
  generate<true>(subtree, aux_vars, oracle, memory, witness, count);
}

} // namespace airbender::witness::memory::delegation